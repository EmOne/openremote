import org.gradle.api.tasks.testing.logging.TestExceptionFormat
import org.gradle.api.tasks.testing.logging.TestLogEvent

apply plugin: "java"
apply plugin: "groovy"

dependencies {

    compile "org.codehaus.groovy:groovy:$groovyVersion"

    compile resolveProject(":manager")

    compile "junit:junit:$junitVersion"
    compile "org.spockframework:spock-core:$spockVersion"

    compile "org.glassfish.tyrus:tyrus-client:$tyrusVersion"
    compile "org.glassfish.tyrus:tyrus-container-grizzly-client:$tyrusVersion"
    compile "org.objenesis:objenesis:$objenesisVersion"
    compile "net.bytebuddy:byte-buddy:$byteBuddyVersion"

    compile("com.github.calimero:calimero-server:$calimeroVersion") {
        // Libraries should not depend on a logging implementation and/or alpha versions!
        exclude group: "org.slf4j"
    }
}

test {
    workingDir = findProject(":openremote") != null ? resolveProject("").projectDir : rootProject.projectDir
    outputs.upToDateWhen {false}
    testLogging {
        // set options for log level LIFECYCLE
        events TestLogEvent.FAILED,
                TestLogEvent.PASSED,
                TestLogEvent.SKIPPED
        exceptionFormat TestExceptionFormat.FULL
        showExceptions true
        showCauses true
        showStackTraces true

        // set options for log level DEBUG and INFO
        debug {
            events TestLogEvent.STARTED,
                    TestLogEvent.FAILED,
                    TestLogEvent.PASSED,
                    TestLogEvent.SKIPPED,
                    TestLogEvent.STANDARD_ERROR
            exceptionFormat TestExceptionFormat.FULL
        }
        info.events = debug.events
        info.exceptionFormat = debug.exceptionFormat

        afterTest { desc, result ->
            logger.quiet "${desc.className} > ${desc.name} took: ${(result.endTime - result.startTime)}ms"
        }

        afterSuite { desc, result ->
            if (!desc.parent) { // will match the outermost suite
                def output = "Results: ${result.resultType} (${result.testCount} tests, ${result.successfulTestCount} passed, ${result.failedTestCount} failed, ${result.skippedTestCount} skipped)"
                def startItem = '|  ', endItem = '  |'
                def repeatLength = startItem.length() + output.length() + endItem.length()
                println('\n' + ('-' * repeatLength) + '\n' + startItem + output + endItem + '\n' + ('-' * repeatLength))
            }
        }
    }
}
